;******************* MUL16f Function *****************
;
; uses 1 stack level
; uses WREG, PRODL, PRODH, TABLAT, FSR0, FSR1, FSR2
;
; 16 bit unsigned multiplication.  Contents of location 
; addressed by FSR0 is multiplied times contents of location
; addressed by FSR1, result is at location addressed by FSR2
;

MUL16f:
	MOVF	INDF0, 0
	MULWF	INDF1
	MOVFF	PRODL, INDF2
	MOVLW	1
	MOVFF	PRODH, PLUSW2

	MOVFF	FSR1L, TABLAT
	MOVF	PLUSW0,0
	MULWF	PREINC1
	MOVLW	2
	MOVFF	PRODL, PLUSW2
	MOVLW	3
	MOVFF	PRODH, PLUSW2
	MOVFF   TABLAT, FSR1L
	
	MOVFF	FSR1L, TABLAT
	MOVF	INDF0, 0
	MULWF	PREINC1
	MOVFF	TABLAT, FSR1L
	MOVFF	FSR2L, TABLAT
	MOVF	PRODL, 0
	ADDWF	PREINC2
	MOVF	PRODH, 0
	ADDWFC	PREINC2
	CLRF	WREG
	ADDWFC	PREINC2
	MOVFF	TABLAT, FSR2L

	MOVF	PREINC0, 0
	MULWF	INDF1
	MOVF	PRODL, 0
	ADDWF	PREINC2
	MOVF	PRODH, 0
	ADDWFC	PREINC2
	CLRF	WREG
	ADDWFC	PREINC2
	RETURN
